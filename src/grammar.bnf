STATEMENT ::=   TYPE_DECLARATION     |
                FUNCTION_DEFINITIONS |
                EPSILON

# Functions

TYPE_DECLARATION ::= IDENTIFIER DOUBLE_DOTS TYPE_LIST

TYPE_LIST ::= TYPE | TYPE ARROW TYPE_LIST

TYPE ::= IDENTIFIER | LBRACKET TYPE RBRACKET | LPARANTHESIS TYPE_LIST RPARANTHESIS

FUNCTION_DEFINITIONS ::= EXPRESSION EQUAL EXPRESSION |
                         EXPRESSION CASE_LIST

CASE_LIST ::= CASE | CASE CASE_LIST

CASE ::= PIPE EXPRESSION EQUAL EXPRESSION

# Expressions

EXPRESSION ::= EXPRESSION_ELEM EXPRESSION | EXPRESSION_ELEM

EXPRESSION_ELEM ::= LPARANTHESIS EXPRESSION RPARANTHESIS | VALUE | OPERATOR

OPERATOR ::= PLUS | MINUS | MUL | DIV | MOD | CONCAT | 
             AND | OR | GREATER_THAN | LESS_THAN | GREATER_OR_EQUAL_THAN | LESS_OR_EQUAL_THAN | 
             EQUALITY | INEQUALITY

# Values

VALUE ::= IDENTIFIER | INTEGER | FLOAT | STRING | ENV_VAR | JSON | BOOL | TYPE_CAST | VALUE_LIST

VALUE_LIST ::= LBRACKET RBRACKET | LBRACKET EXPRESSION_ELEM VALUE_LIST_R RBRACKET

VALUE_LIST_R ::= COMMA EXPRESSION_ELEM VALUE_LIST_R | EPSILON

ENV_VAR ::= DOLLLAR IDENTIFIER

TYPE_CAST ::= LESS_THAN TYPE_LIST GREATER_THAN

# JSON

JSON ::= LCURLYBRACES JSON_KV_LIST RCURLYBRACES

JSON_KV_LIST ::= JSON_KV | JSON_KV COMMA JSON_KV_LIST

JSON_KV ::= IDENTIFIER DOTS JSON | 
            IDENTIFIER DOTS JSON_VALUE  | 
            IDENTIFIER DOTS JSON_VALUE_LIST

JSON_VALUE_LIST ::= LBRACKET RBRACKET | LBRACKET JSON_VALUE JSON_VALUE_LIST_R RBRACKET

JSON_VALUE_LIST_R ::= COMMA JSON_VALUE JSON_VALUE_LIST_R | EPSILON

JSON_VALUE ::=  JSON_VALUE_LIST | JSON | IDENTIFIER | 
                INTEGER | FLOAT | STRING | ENV_VAR | BOOL | 
                LPARANTHESIS EXPRESSION RPARANTHESIS